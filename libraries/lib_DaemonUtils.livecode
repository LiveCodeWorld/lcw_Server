script "lib_DaemonUtils"
--> MetaData
-
name: lib_DaemonUtils
type: behavior
version: 0.3
copyright: David Bovill
licence:  GPLv3

/*
Put scripts here that aid the speed of scripting a server,
but that eventually shoud be factored out (for speed reasons) of the final server,
or at least bundled in with the server script.

It is placed at the very back of the behavior chain (behind lib_Log),
or it could go before non-daemon libs like lib_Log
*/

--> Fedwiki | Routes
-
/*
These handlers are now mainly superceded by array based standardised daemon routing?
*/

command fedwiki_NormlizeRoute @someRoute
   switch someRoute
      /*
      case "/"
         put "/view/welcome-visitors" into someRoute
         break
         */
      case "/view/welcome-visitors.html"
         put "/view/welcome-visitors" into someRoute
         break
      case "/welcome-visitors"
         put "/view/welcome-visitors" into someRoute
         break
      case "/welcome-visitors.html"
         put "/view/welcome-visitors" into someRoute
         break
      case "/"
         put "/view/welcome-visitors" into someRoute
         break
   end switch
end fedwiki_NormlizeRoute

function fedwiki_GetRouteData someRoute
   fedwiki_NormlizeRoute someRoute
   put daemon_GetRouteData (someRoute) into someData
   return someData
end fedwiki_GetRouteData

command fedwiki_SetRouteData someRoute, someData
   fedwiki_NormlizeRoute someRoute
   daemon_SetRouteData someRoute, someData
   return the result
end fedwiki_SetRouteData



--> Files | Data
-
-- getprop daemon_RootFolder
return fedwiki_GetRootFolder()
end daemon_RootFolder

function fedwiki_GetBinaryFile shortFile
   put fedwiki_GetFile (shortFile) into someFile
   if there is not a file someFile then
      fedwiki_Log ("File does not exist:" && shortFile)
      return empty
   end if
   put "binfile:" & someFile into someUrl
   put url someUrl into someData
   return someData
end fedwiki_GetBinaryFile

function fedwiki_GetTextFile shortFile
   put fedwiki_GetFile (shortFile) into someFile
   if there is not a file someFile then
      fedwiki_Log ("File does not exist:" && shortFile)
      return empty
   end if
   put "file:" & someFile into someUrl
   put url someUrl into someText
   return someText
end fedwiki_GetTextFile

function fedwiki_GetFileData shortFile
   set the itemdelimiter to "."
   put item - 1 of shortFile into someFileExtension
   switch someFileExtension
      case "png"
         put "binfile:" & fedwiki_GetFile (shortFile) into someUrl
         break
      default -- case "js"
         put "file:" & fedwiki_GetFile (shortFile) into someUrl
         break
   end switch
   put url someUrl into someData
   return someData
end fedwiki_GetFileData


--> Local | Folders
-
command fedwiki_SetLocalFolders pBigDataFolder
   /*
   A utility to set all three folders 
   by simply chosing or setting one overarching "bigData folder" and creating sesnible defaults
   */
   fedwiki_SetBigDataFolder pBigDataFolder
   put the result into pBigDataFolder
   
   -- now we have the top level pBigDataFolder
   -- lets set standard inner folders for the .wiki folder and dowloaded jaon files
   put pBigDataFolder & "/wiki/" into localWikiFolder
   fedwiki_SetLocalWikiFolder localWikiFolder
   put pBigDataFolder & "/site_json/" into localSiteJsonFolder
   fedwiki_SetLocalSiteJsonFolder localSiteJsonFolder
end fedwiki_SetLocalFolders

function fedwiki_GetFile shortFile
   put fedwiki_GetWelcomeFolder() into indexFileFolder
   put indexFileFolder & shortFile into someFile
   return someFile
end fedwiki_GetFile

getprop fedwiki_Files
   -- put myPath & "Welcome Visitors.html" into indexFile
   put fedwiki_GetWelcomeFolder() into indexFileFolder
   put folder_ShortFiles (indexFileFolder) into shortFiles
   return shortFiles
end fedwiki_Files

function fedwiki_GetWelcomeFolder
   -- this is for when you use a browser to download standalone web page for w Welcome page.
   put fedwiki_GetRootFolder() into rootFolder
   put rootFolder & "Welcome Visitors_files/" into indexFileFolder
   return indexFileFolder
end fedwiki_GetWelcomeFolder

-- getprop fedwiki_LocalWikiRoot
put pref_GetValue ("fedwiki_LocalWikiFolder") into localWikiFolder
end fedwiki_LocalWikiRoot

command fedwiki_SetLocalWikiFolder localWikiFolder
   if localWikiFolder is empty then
      answer folder "Choose a folder for the local wiki-page json files..."
      put it into localWikiFolder
      if localWikiFolder is empty then
         exit to top
      end if
   end if
   
   folder_Format localWikiFolder
   pref_SetValue "fedwiki_LocalWikiFolder", localWikiFolder
   return localWikiFolder
end fedwiki_SetLocalWikiFolder


--> Fedwiki | Local | Site Json | Files
-
/*
The original site JSON files should be stored as a backup, before they are parsed into arrays, and spat out as original JSON files.
That way if there is a script error we can always go back to the original JSON files as a backup.
We also store the arrays created from the Json files in the same folder.
*/

function fedwiki_LocalSiteJsonFile fedwikiDomain
   put fedwiki_GetLocalSiteJsonFolder() into localSiteJsonFolder
   put localSiteJsonFolder & fedwikiDomain & ".json" into someFile
   return someFile
end fedwiki_LocalSiteJsonFile

function fedwiki_GetLocalSiteJsonFolder
   put pref_GetValue ("fedwiki_LocalSiteJsonFolder") into localSiteJsonFolder
   
   if there is not a folder localSiteJsonFolder then
      answer folder "Choose a folder for the local wiki-page json files..."
      put it into localSiteJsonFolder
      if localSiteJsonFolder is empty then exit to top
      fedwiki_SetLocalSiteJsonFolder localSiteJsonFolder
      put the result into localSiteJsonFolder -- here we make sure it has a trailing "/"
   end if
   return localSiteJsonFolder
end fedwiki_GetLocalSiteJsonFolder

command fedwiki_SetLocalSiteJsonFolder localSiteJsonFolder
   folder_Format localSiteJsonFolder
   pref_SetValue "fedwiki_LocalSiteJsonFolder", localSiteJsonFolder
   return localSiteJsonFolder
end fedwiki_SetLocalSiteJsonFolder

function fedwiki_GetRootFolder
   set the itemdelimiter to "/"
   put the filename of me into rootFolder
   put empty into item -1 of rootFolder
   return rootFolder
end fedwiki_GetRootFolder

function fedwiki_GetWelcomeFile
   put fedwiki_GetRootFolder() into rootFolder
   put rootFolder & "Welcome Visitors.html" into indexFile
   return indexFile
end fedwiki_GetWelcomeFile


--> Utils
-
getprop script_MessageHandlers
   local mHandlerNames
   put the script of the target into someScript
   put the revAvailableHandlers of the target into handlerTable
   repeat for each line tableLine in handlerTable
      if word 1 of tableLine = "M" then
         put word 3 of tableLine into startLineNum
         get line startLineNum of someScript
         if word 1 of it = "on" then
            put word 2 of tableLine into mHandlerName
            put mHandlerName & CR after mHandlerNames
         end if
      else
         delete char -1 of mHandlerNames
         return mHandlerNames
      end if
   end repeat
   delete char -1 of mHandlerNames
   return mHandlerNames
end script_MessageHandlers

getprop daemon_RouteHandlers
   -- as fast as possible
   local dHandlerNames
   put the script of the target into someScript
   put the revAvailableHandlers of the target into handlerTable
   repeat for each line tableLine in handlerTable
      if word 1 of tableLine = "M" then
         put word 2 of tableLine into hName
         if hName begins with "daemon_" then next repeat
         put hName & CR after dHandlerNames
      else
         delete char -1 of dHandlerNames
         return dHandlerNames
      end if
   end repeat
   delete char -1 of dHandlerNames
   return dHandlerNames
end daemon_RouteHandlers

function daemon_ConstructDotHandler requestArray
   -- return a handler made from the reguest url's path and method
   put requestArray ["SCRIPT_NAME"] into someRoute
   put requestArray ["Request Method"] into someMethod
   
   split someRoute by "/"
   combine someRoute with "."
   set the itemdelimiter to "."
   
   put someMethod & someRoute into messageName
   replace "-" with "_" in messageName
   return messageName
end daemon_ConstructDotHandler


--> Request | Utils
-
/*
These handlers simply make it easier to remember the way to extract the value from the requestArray.
Better would be to simply name the array keys better?
*/

function request_SocketID requestArray
   return requestArray ["SocketID"]
end request_SocketID

function request_Method requestArray
   return requestArray ["Request Method"]
end request_Method

function request_UrlPath requestArray
   return requestArray ["SCRIPT_NAME"]
end request_UrlPath

function request_QueryString requestArray
   return requestArray ["QUERY_STRING"]
end request_QueryString
