script "lib_RemoteServer"
--> MetaData
-
license: GPLv3
name: lib_RemoteServer
type: script library
version: 0.2

/*
This library  is for manipulating a remote server using ssh and scp.
It should work on the desktop or from server to server.

As there are no passwords (it relies on public/private key access on the server), 
and REST accesspoints can be secured using DreamFactory, 
this is much more secure than scripting things directly in LiveCode.

== To Do ==
At the moment we use local variables for speed.
I don't think spped is a requirement for server oepratiosn. Security is more important.
We should instead use local file array based access for simplicity (and therefore secirity)
Remove this loca vairable stuff and utilise functions that access sotred arrays instead.
*/


--> Variables
-
constant LCW_ServerURL = "rest.livecode.world"
local LocalArray


--> Working on
-
function server_FetchPrefArray pProjectName
   if pProjectName is empty then put "Default" into pProjectName
   put pref_FetchValue ("server_Array", pProjectName) into serverPrefArray
   return serverPrefArray
end server_FetchPrefArray

command server_SavePrefArray serverPrefArray, pProjectName
   if pProjectName is empty then put "Default" into pProjectName
   pref_SaveValue "server_Array", serverPrefArray, pProjectName
   put the result into dataArray
   return dataArray
end server_SavePrefArray


--> Server | Remote | Upload
-
command server_UploadStacks stackNames
   repeat for each line stackName in stackNames
      set the cursor to busy
      server_UploadStack stackName
   end repeat
end server_UploadStacks

command server_UploadStack stackName, pDontNotify
   put the filename of stack stackName into stackPath
   set the itemdelimiter to "/"
   put item -1 of stackPath into shortPath
   put rig_StackPath (shortPath) into serverStackPath
   
   put the name of stack stackName into stackObject
   save stackObject
   
   put server_GetURL() into serverAddress
   put server_GetSshUser() into sshUser
   put server_GetSshKeyPath() into sshKeyPath
   server_StoreFile stackPath, serverStackPath, serverAddress, sshUser, sshKeyPath
   put the result into someResult
   
   if pDontNotify is not true then
      put merge ("Uploaded stack" && shortPath && "to [[serverAddress]]!") into someComment
      return someComment
   end if
   return someResult
end server_UploadStack



--> Server | Remote | Model | Utils
-
command server_SetURL serverURL
   -- use to switch server configuration
   put serverURL into LocalArray ["serverURL"]
end server_SetURL

function server_GetURL
   put LocalArray ["serverURL"] into serverURL
   if serverURL is empty then put LCW_ServerURL into serverURL
   return serverURL
end server_GetURL

function server_GetSshUser pServerURL
   return server_GetProperty ("sshUser", pServerURL)
end server_GetSshUser

function server_GetSshKeyPath pServerURL
   return server_GetProperty ("sshKeyPath", pServerURL)
end server_GetSshKeyPath

function server_GetDropletName pServerURL
   return server_GetProperty ("dropletName", pServerURL)
end server_GetDropletName

function server_GetIPaddress pServerURL
   return server_GetProperty ("serverAddress", pServerURL)
end server_GetIPaddress


--> Server | Remote | Model
-
function server_GetProperty someKey, pServerURL
   put server_GetArray (pServerURL) into serverArray
   put serverArray [someKey] into someValue
   return someValue
end server_GetProperty

command server_SetProperty someKey, someValue, pServerURL
   put server_GetArray (pServerURL) into serverArray
   put someValue into serverArray [someKey]
   server_SetArray serverArray, pServerURL
   return serverArray
end server_SetProperty
   
function server_GetArray pServerURL
   if pServerURL is empty then put server_GetURL() into pServerURL
   --
   put server_FetchPrefArray () into serverPrefArray
   put serverPrefArray [pServerURL] into serverArray
   return serverArray
end server_GetArray

command server_SetArray serverArray, pServerURL
   if pServerURL is empty then put server_GetURL() into pServerURL
   --
   put server_FetchPrefArray () into serverPrefArray
   put serverArray into serverPrefArray [pServerURL]
   server_SavePrefArray serverPrefArray
   return serverPrefArray
end server_SetArray

command server_DeleteArray serverURL
   if pServerURL is empty then put server_GetURL() into pServerURL
   -- 
   put server_FetchPrefArray() into serverArray
   delete variable serverArray [serverURL]
   server_SavePrefArray serverArray
end server_DeleteArray


--> Server | Shell | Wrappers
-
command server_StoreData someData, pathToStore, serverAddress, sshUser, pSshKeyPath
   -- uses "scp" and private keys
   put the tempname into localFile
   put someData into url ("binfile://" & localFile)
   server_StoreFile localFile, pathToStore, serverAddress, sshUser
   put the result into shellResult
   return shellResult
end server_StoreData


--> Server | Shell
-
function server_FetchFileArray serverAddress, sshUser, serverPath, pSshKeyPath
   if the environment = "server" then
      breakpoint
   else
      put server_FetchFileList (serverAddress, serverPath, true, sshUser, pSshKeyPath) into lsResult
      switch lsResult
         case "Host key verification failed."
            /*
            This is usually because the server fingerprint has changed. need to research more about whu this happens.
            Head over to the terminal and try:
            
            The authenticity of host 'rest.livecode.world (188.166.10.234)' can't be established.
            ECDSA key fingerprint is SHA256:ZN82d23HRwxbvElysl8dHvTsCFHwfnsv+xUKL5EeLGQ.
            Are you sure you want to continue connecting (yes/no)? yes
            Warning: Permanently added 'rest.livecode.world' (ECDSA) to the list of known hosts.
            */
            return exmpty
         default
            put shell_ConstructLSArray (lsResult, true) into fileArray
      end switch
   end if
   return fileArray
end server_FetchFileArray

function server_FetchFileList serverAddress, serverPath, pListAll, sshUser, pSshKeyPath
   put "ssh" into someShell
   if pSshKeyPath is not empty then
      shell_BashParamEscape pSshKeyPath
      put space & "-i " && pSshKeyPath after someShell
   end if
   
   put space & sshUser & "@" & serverAddress && "ls" after someShell
   if pListAll is true then put space &  "-la" after someShell
   
   shell_BashParamEscape serverPath
   put space & serverPath after someShell
   
   put word 1 to -1 of shell (someShell) into fileListing
   return fileListing
end server_FetchFileList

function server_FileInfoArray someFile
   put server_GetURL() into remoteDomain
   put server_GetSshUser() into sshUser
   
   put someFile into bashFile
   shell_BashParamEscape bashFile
   put "ls -la" && bashFile into remoteShell
   ssh_DoRemoteShell sshUser, remoteDomain, remoteShell
   put the result into lsResult
   
   put lsResult into fileArray ["ls_la_Result"]
   switch
      case word 1 of lsResult = "total"
         -- it's a folder
         put someFile into fileArray ["file"]
         put char 1 of line 2 of of lsResult = "d" into fileArray ["isFolder"]
         return fileArray
      case lsResult ends with ": No such file or directory"
         -- file or folder is missing
         return fileArray
      default
         put shell_ConstructLSArray (lsResult, true) into lsArray
         put lsArray ["file"] into filesArray
         put line 1 of keys (filesArray) into filePath
         put filesArray [filePath] into fileArray
         
         put filePath into fileArray ["file"]
         put remoteDomain into fileArray ["domain"]
         
         set the itemdelimiter to "/"
         put item -1 of filePath into shortFile
         put shortFile into fileArray ["shortFile"]
         
         if shortFile contains "." then
            set the itemdelimiter to "."
            put item -1 of shortFile into fileArray ["fileExtension"]
         else
            put empty into fileArray ["fileExtension"]
         end if
         return fileArray
   end switch
end server_FileInfoArray

function server_FetchFile pathToFetch, serverAddress, sshUser
   if char 1 of pathToFetch is "/" then
      put pathToFetch into fullPath
   else
      put "~/" & pathToFetch into fullPath
   end if
   shell_BashParamEscape fullPath
   
   put ssh_FetchFile (sshUser, serverAddress, fullPath) into someData
   set the itemdelimiter to ":"
   if (item 1 of someData = "cat" and item -1 of someData = " No such file or directory") then return empty
   return someData
end server_FetchFile

command server_StoreFile localFile, pathToStore, serverAddress, sshUser, pSshKeyPath
   -- uses "scp" and private keys for security
   if serverAddress is empty or sshUser is empty then
      return "Error, serverAddress is empty or sshUser is empty"
   end if
   
   if char 1 of pathToStore is "/" then
      put pathToStore into fullPath
   else
      put "~/" & pathToStore into fullPath
   end if
   shell_StoreFile sshUser, serverAddress, fullPath, localFile, pSshKeyPath
   put the result into shellResult
   return shellResult
end server_StoreFile

command server_DeleteFile pathToDelete, serverAddress, sshUser
   -- do we want to check if the file exists?
   if serverAddress is empty or sshUser is empty then
      return "Error, serverAddress is empty or sshUser is empty"
   end if
   
   -- relatively safe no recursive delete
   -- put "rm" && somePath into remoteCommand 
   ssh_DeleteFile sshUser, serverAddress, pathToDelete
   put the result into shellResult
   return shellResult
end server_DeleteFile
